buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }

}

repositories {
    maven {
        name 'CB Repo'
        url "http://chickenbones.net/maven/"
    }
    maven {
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven {
        name = "ic2-and-forestry"
        url = "http://maven.ic2.player.to/"
    }
	ivy {
        name 'gtnh_download_source'
        artifactPattern "http://downloads.gtnewhorizons.com/Mods_for_Jenkins/[module]-[revision].[ext]"
    }
}

apply plugin: 'forge'

group = mod_group
archivesBaseName = mod_archive_name

minecraft {
    runDir = "run"
    version = "${minecraft_version}-${forge_version}"
    replaceIn mod_info_path
    replace "@MAJOR@", major_version
    replace "@MINOR@", minor_version
    replace "@PATCH@", patch_version
    replace "@FORGE_VERSION@", forge_version
}
dependencies {
    compile "codechicken:CodeChickenLib:${minecraft_version}-${CCLIB_version}:dev"
    compile "codechicken:CodeChickenCore:${minecraft_version}-${CCC_version}:dev"
    compile "codechicken:NotEnoughItems:${minecraft_version}-${NEI_version}:dev"
    compile "mcp.mobius.waila:Waila:${WAILA_version}_${minecraft_version}:dev"
    compile "net.industrial-craft:industrialcraft-2:${IC2_version}:dev"
    compile ("appeng:appliedenergistics2:${AE2_version}:dev"){
        transitive = false
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        expand 'version':"${major_version}.${minor_version}.${patch_version}", 'mcversion':minecraft_version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
    }
}

version = "${minecraft_version}-${major_version}.${minor_version}.${patch_version}"

jar {
    manifest {
        attributes 'FMLCorePlugin': 'remoteio.core.LoadingPlugin'
        attributes 'FMLCorePluginContainsFMLMod': 'true'
    }

    classifier = 'universal'
}

sourceSets {
    main {
        java {
            srcDirs += "src/api/java"
            exclude 'thaumcraft'
        }
    }
}
